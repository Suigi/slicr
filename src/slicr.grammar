@top Program { (Statement | newline)* }

Statement {
  SliceStatement |
  NodeStatement |
  EdgeStatement |
  BoundaryStatement |
  DataStatement
}

SliceStatement {
  kw<"slice"> String
}

NodeStatement {
  ArtifactRef String?
}

IncomingClause {
  DependsArrow ArtifactRef (Comma ArtifactRef)*
}

OutgoingClause {
  ForwardArrow ArtifactRef (Comma ArtifactRef)*
}

EdgeStatement {
  IncomingClause |
  OutgoingClause
}

BoundaryStatement {
  BoundaryMarker
}

ArtifactRef {
  RmRef | UiRef | CmdRef | EvtRef | ExcRef | AutRef | ExtRef | GenericRef
}

RmRef { kw_rm Colon RmName Version? }
UiRef { kw_ui Colon UiName Version? }
CmdRef { kw_cmd Colon CmdName Version? }
EvtRef { kw_evt Colon EvtName Version? }
ExcRef { kw_exc Colon Identifier Version? }
AutRef { kw_aut Colon Identifier Version? }
ExtRef { kw_ext Colon Identifier Version? }
GenericRef { Identifier Colon Identifier Version? }

RmName { Identifier }
UiName { Identifier }
CmdName { Identifier }
EvtName { Identifier }
Version { At Number }

DataStatement {
  kw<"data"> Colon JsonObject?
}

@tokens {
  Identifier { $[a-zA-Z_] $[a-zA-Z0-9_\-#]* }
  String { '"' (!["\\] | "\\" _) * '"' }
  Number { $[0-9]+ ("." $[0-9]+)? }
  
  DependsArrow { "<-" }
  ForwardArrow { "->" }
  BoundaryMarker { "---" }
  Colon { ":" }
  At { "@" }
  BracketL { "[" }
  BracketR { "]" }
  BraceL { "{" }
  BraceR { "}" }
  Comma { "," }

  space { $[ \t]+ }
  newline { $[\r\n]+ }

  @precedence { DependsArrow, Identifier }
  @precedence { space, Identifier }
}

kw_rm { @specialize[@name=rmType]<Identifier, "rm"> }
kw_ui { @specialize[@name=uiType]<Identifier, "ui"> }
kw_cmd { @specialize[@name=cmdType]<Identifier, "cmd"> }
kw_evt { @specialize[@name=evtType]<Identifier, "evt"> }
kw_exc { @specialize[@name=excType]<Identifier, "exc"> }
kw_aut { @specialize[@name=autType]<Identifier, "aut"> }
kw_ext { @specialize[@name=extType]<Identifier, "ext"> }

kw<term> { @specialize[@name={term}]<Identifier, term> }

// Simplified JSON rules for the one-line 'data' block.
JsonObject { BraceL (Property (Comma Property)*)? BraceR }
Property { (String | Identifier) Colon Value }
Value { String | Number | JsonObject | JsonArray | kw<"true"> | kw<"false"> | kw<"null"> }
JsonArray { BracketL (Value (Comma Value)*)? BracketR }

@skip { space }
